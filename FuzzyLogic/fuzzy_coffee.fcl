FUNCTION_BLOCK
VAR_INPUT
water_temperature : REAL;
water_petrification : REAL;
day_time : REAL;
sugar_level : REAL;
END_VAR

VAR_OUTPUT
preparing_time : REAL;
END_VAR

FUZZIFY water_temperature
TERM cold := (0, 1) (10, 1) (15, 0.60) (20, 0.20) (22.5, 0);
TERM lukewarm := (12.5, 0) (15, 0.20) (20, 0.60) (25, 1) (35, 1) (40, 0.60) (45, 0.2) (47.5, 0);
TERM hot := (35, 0) (37.5, 0.20) (42.5, 0.60) (47.5, 1) (57.5, 1) (62.5, 0.6) (67.5, 0.2) (70, 0);
TERM boiling := (60, 0) (62.5, 0.20) (67.5, 0.60) (72.5, 1) (77.5, 1) (100, 1);
END_FUZZIFY

FUZZIFY water_petrification
TERM none := (0, 1) (1, 0.70) (2, 0.4) (3, 0.1) (3.33, 0) (4, 0);
TERM slight := (0, 0) (1, 0) (1.33, 0.1) (2.33, 0.40) (3.33, 0.70) (4.33, 1) (5,1) (6, 0.70) (7, 0.40) (8, 0.10) (8.33, 0);
TERM average := (5, 0) (5.33, 0.1) (6.33, 0.4) (7.33, 0.7) (8.33, 1) (9, 1) (10, 0.70) (11, 0.40) (12, 0.1) (12.33, 0);
TERM enormous := (8,0) (10, 0.20) (12, 0.40) (14, 0.6) (16, 0.8) (22, 1) (100, 1);
END_FUZZIFY

FUZZIFY day_time
TERM morning := (1, 0) (3, 0.5) (5, 1) (7, 1) (9, 0.50) (11, 0);
TERM midday := (7, 0) (9, 0.50) (11, 1) (13, 1) (15, 0.50) (17, 0);
TERM afternoon := (13, 0) (15, 0.50) (17, 1) (19, 1) (21, 0.50) (23, 0);
TERM night := (0,1) (1, 1) (3, 0.50) (5, 0) (19, 0) (21, 0.50) (23, 1);
END_FUZZIFY

FUZZIFY sugar_level
TERM none := (0, 1) (1, 0.66) (2, 0.33) (3, 0);
TERM slight := (0, 0) (1, 0.33) (2, 0.66) (3, 1) (4, 1) (5, 0.66) (6, 0.33) (7, 0);
TERM average := (3, 0) (4, 0.33) (5, 0.66) (6, 1) (7, 1) (9, 0.66) (11, 0.33) (13, 0);
TERM enormous := (7,0) (8, 0.20) (9, 0.40) (10, 0.6) (11, 0.8) (12, 1) (20, 1);
END_FUZZIFY

DEFUZZIFY preparing_time
TERM short := (0, 1) (20, 1) (27.50, 0.50) (35, 0) (50, 0);
TERM normal := (20, 0) (27.5, 0.50) (35, 1) (45, 1) (52.5, 0.5) (60, 0) (70, 0);
TERM long := (45, 0) (52.5, 0.50) (60, 1) (80, 1) (100, 1);
METHOD : COG;
DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK first
AND: MIN;
ACT: MIN;

RULE 0: IF water_petrification IS enormous THEN preparing_time IS long;

RULE 1: IF water_petrification IS average THEN preparing_time IS long;

RULE 2: IF water_petrification IS slight AND water_temperature IS cold THEN preparing_time IS long;
RULE 3: IF water_petrification IS slight AND water_temperature IS lukewarm THEN preparing_time IS normal;
RULE 4: IF water_petrification IS slight AND water_temperature IS hot THEN preparing_time IS normal;
RULE 5: IF water_petrification IS slight AND water_temperature IS boiling THEN preparing_time IS normal;

RULE 6: IF water_petrification IS none AND water_temperature IS cold AND day_time IS morning AND sugar_level IS enormous THEN preparing_time IS long;
RULE 7: IF water_petrification IS none AND water_temperature IS cold AND day_time IS morning AND sugar_level IS average THEN preparing_time IS normal;
RULE 8: IF water_petrification IS none AND water_temperature IS cold AND day_time IS morning AND sugar_level IS slight THEN preparing_time IS normal;
RULE 9: IF water_petrification IS none AND water_temperature IS cold AND day_time IS morning AND sugar_level IS none THEN preparing_time IS normal;

RULE 10: IF water_petrification IS none AND water_temperature IS cold AND day_time IS midday THEN preparing_time IS normal;
RULE 11: IF water_petrification IS none AND water_temperature IS cold AND day_time IS afternoon THEN preparing_time IS normal;
RULE 12: IF water_petrification IS none AND water_temperature IS cold AND day_time IS night THEN preparing_time IS normal;


RULE 13: IF water_petrification IS none AND water_temperature IS lukewarm AND day_time IS morning THEN preparing_time IS normal;
RULE 14: IF water_petrification IS none AND water_temperature IS lukewarm AND day_time IS midday THEN preparing_time IS normal;
RULE 15: IF water_petrification IS none AND water_temperature IS lukewarm AND day_time IS afternoon THEN preparing_time IS short;
RULE 16: IF water_petrification IS none AND water_temperature IS lukewarm AND day_time IS night THEN preparing_time IS short;

RULE 17: IF water_petrification IS none AND water_temperature IS hot THEN preparing_time IS short;
RULE 18: IF water_petrification IS none AND water_temperature IS boiling THEN preparing_time IS short;

END_RULEBLOCK

END_FUNCTION_BLOCK